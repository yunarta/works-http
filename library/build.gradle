apply plugin: 'com.android.library'
apply plugin: 'signing'

group 'com.mobilesolutionworks'
version '1.0.5'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    useLibrary 'org.apache.http.legacy'

    def androidTargetSdkVersion = compileSdkVersion

    if (project.hasProperty('androidConfiguration')) {
        compileSdkVersion Integer.parseInt(project.androidCompileSdkVersion)
        buildToolsVersion project.androidBuildToolsVersion
        androidTargetSdkVersion = project.androidTargetSdkVersion
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion androidTargetSdkVersion
        versionCode 2
        versionName "1.0.5"
    }

    buildTypes {
        release {
//            runProguard false
//            if (runProguard) {
//                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
//            }
        }
    }
}

dependencies {
    compile project(':works-util')
}

def generatePom() {
    def pom = pom {
        project {
            name 'works-http'
            packaging 'aar'
            groupId project.group
            artifactId project.name
            version project.version
            description 'Works Http for Android'
            inceptionYear '2014'
            url 'http://sg.linkedin.com/in/yunarta/'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                    distribution 'repo'
                }
            }

            scm {
                url "https://github.com/yunarta/works-http"
                connection "scm:git:git@github.com:yunarta/works-http.git"
                developerConnection "scm:git:git@github.com:yunarta/works-http.git"
            }


            developers {
                developer {
                    id 'yunarta'
                    name 'Yunarta Kartawahyudi'
                    email 'yunarta.kartawahyudi@gmail.com'
                    url 'http://sg.linkedin.com/in/yunarta/'
                    roles {
                        role 'author'
                        role 'developer'
                    }
                    timezone '+8'
                }
            }
        }
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    if (project.hasProperty('sonatypeUsername')) {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }
                pom = generatePom()

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }
        }
    }
}

android.libraryVariants.all { variant ->
    if (project.hasProperty('androidSdkDir')) {
        def name = variant.buildType.name
        if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
            return;
        }

        task("javadoc${variant.name.capitalize()}", type: Javadoc) {
            description "Generates Javadoc for $variant.name."
            source = variant.javaCompile.source
            ext.androidJar = project.androidSdkDir + "/platforms/${android.compileSdkVersion}/android.jar"
            classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        }

        task("bundleJavadoc${variant.name.capitalize()}", type: Jar) {
            description "Bundles Javadoc into zip for $variant.name."
            classifier = "javadoc"
            from tasks["javadoc${variant.name.capitalize()}"]
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task jar(type: Jar) {
    from 'build/intermediates/classes/release'
}

task javadocJar(type: Jar, dependsOn: "javadocRelease") {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives jar
}
dependencies {
    compile 'cz.msebera.android:httpclient:4.3.6'
}